/*
Copyright 2022-2023 Daniele Zelante  <zeldan@zetasys.net>

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*/


import java.util.regex.Pattern


plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.48.0'
}


dependencies {
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'javax.money:money-api:1.1'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.poi:poi:5.2.4'
    implementation 'org.apache.poi:poi-ooxml:5.2.4'
    implementation 'org.javamoney.moneta:moneta-core:1.4.2'

    testImplementation 'junit:junit:4.13.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}


def run(String cmd) {
    def s = new StringBuilder()
    def p = cmd.execute()
    p.waitForProcessOutput(s, System.err)
    return s.toString().trim()
}

def getRevision() {
    def a = run("git describe --tags --long --exclude *-*")
    if (a == '')
        return 'devel';
    def b = run("git branch --show-current")
    if (b == '')
        return 'devel'
    if (b == 'main')
        return a;
    def z = a.replaceFirst(Pattern.compile('-g.*$'),'')
    def h = a.replaceFirst(Pattern.compile('^.*-g'),'')
    return "$z-$b-g$h"
}

def getBranch() {
    def x = run("git branch --show-current")
    return x == '' ? 'main' : x
}



def getTimestamp() {
    return new Date().format('yyyy-MM-dd HH:mm:ss')
}


def revision = getRevision()
def branch = getBranch()
def timestamp = getTimestamp()
group = 'net.zetasys'

repositories {
   mavenLocal()
   mavenCentral()
}

version = revision.replaceFirst(Pattern.compile('-g.*$'),'')
description = 'zava'


processResources {
    filesMatching('**/*build.properties') {
        expand(
            'z_timestamp': timestamp,
            'z_scmversion': revision,
            'z_scmbranch': branch,
        )
    }
}


compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.compilerArgs << "-Xlint:unchecked,deprecation"
}


publishing {
    publications {
        myLibrary(MavenPublication) {
            from(components.java)
        }
    }
    
	repositories {
		maven {
			name = "gitea"
			url = "https://zetasys.net/gitea/api/packages/zetasys.net/maven"
			credentials {
				username = project.findProperty("gitea.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gitea.key") ?: System.getenv("TOKEN")
			}
	    }
    }
}
    


