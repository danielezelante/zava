/*
Copyright 2022 Daniele Zelante  <zeldan@zetasys.net>

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*/


import java.util.regex.Pattern


plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.ben-manes.versions' version '0.46.0'
}


dependencies {
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'javax.money:money-api:1.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.poi:poi:5.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.2.3'
    implementation 'org.javamoney.moneta:moneta-core:1.4.2'

    testImplementation 'junit:junit:4.13.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}


def getBranch() {
    def x = "git branch --show-current".execute().text.trim()
    return x == '' ? 'main' : x
}


def getRevision() {
    def v = "git describe --tags --long --match v*".execute().text.trim()
        .replaceFirst(Pattern.compile('-g.*'), '')
    if (v != '') {
        def n = v.replaceFirst(Pattern.compile('.*-'), '')
        if (n == '0')
            return v
                .replaceFirst(Pattern.compile('-.*'), '')
                .replaceFirst(Pattern.compile('^v'), '')
    }

    def a = "git describe --tags --long --match b*".execute().text.trim()
        .replaceFirst(Pattern.compile('-g'), '+g')
        .replaceFirst(Pattern.compile('^b'), '')

    if (a != '')
        return a

    return 'devel'
}


def getTimestamp() {
    return new Date().format('yyyy-MM-dd HH:mm:ss')
}


def revision = getRevision()
def branch = getBranch()
def timestamp = getTimestamp()
group = 'net.zetasys'

repositories {
   mavenLocal()
   mavenCentral()
}

version = revision
description = 'zava'


processResources {
    filesMatching('**/*build.properties') {
        expand(
            'z_timestamp': timestamp,
            'z_scmversion': revision,
            'z_scmbranch': branch,
        )
    }
}


compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}


publishing {
    publications {
        myLibrary(MavenPublication) {
            from(components.java)
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/danielezelante/zava"
            credentials {
                username = System.getenv("GH_USERNAME")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
    
}

